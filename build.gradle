import net.xolt.freecam.gradle.BumpVersionTask
import org.jetbrains.changelog.tasks.BaseChangelogTask

plugins {
    alias(libs.plugins.architectury.plugin)
    alias(libs.plugins.architectury.loom) apply false
    alias(libs.plugins.shadow) apply false
    id "org.jetbrains.changelog"
}

tasks.named('wrapper') {
    // Use "all" so we get sources and javadoc too
    distributionType = "all"
    gradleVersion = "8.11"
}

// Helper task that bumps the version number
tasks.register("bumpVersion", BumpVersionTask) {
    group = "version"
    input = file("gradle.properties")
    key = "mod_version"
}

// Move the changelog tasks to the "version" group
tasks.withType(BaseChangelogTask).configureEach {
    group = "version"
}

// Don't need these on the root project
afterEvaluate {
    tasks.jar.enabled = false
    tasks.sourcesJar.enabled = false
}

architectury {
    minecraft = rootProject.minecraft_version
}

changelog {
    // Use the mod_version in gradle.properties as the release version/tag.
    // Build tag/diff links using the github repo URL.
    version = project.mod_version
    repositoryUrl = project.source_code_url

    // Title & intro are printed right at the start of the changelog.
    // The values here will replace whatever exists in the file when patching.
    title = "Changelog"
    introduction = """
        All notable changes to this project will be documented in this file.
        
        This file is formatted as per [Keep a Changelog](https://keepachangelog.com/en/1.0.0),
        and Freecam's versioning is based on [Semantic Versioning](https://semver.org/spec/v2.0.0.html).
        """.stripIndent()

    // Group sub-headings added to the "unreleased" section when patching the changelog.
    // Other sub-headings can still be added manually, if required.
    groups = [
            "Added",
            "Changed",
            "Removed",
            "Fixed",
    ]

    // Regex used to find versions in headings.
    // The default regex only supports semantic versions, this one is more lenient.
    headerParserRegex = ~/(\d+(?:\.\d+)+(?:-[-a-z]+\d*)?)/
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    version = "${project.mod_version}+mc${project.minecraft_version}"
    group = rootProject.maven_group
    base {
        // Format archive names, e.g. 'freecam-fabric-1.2.3+mc1.20.4.jar'
        archivesName = rootProject.name + project.path.replaceAll(':', '-')
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        mavenCentral()
        maven { url "https://maven.parchmentmc.org/" }
        maven { url "https://maven.neoforged.net/releases/" }
        if (rootProject.neoforge_pr != "") {
            maven {
                url "https://prmaven.neoforged.net/NeoForge/pr${rootProject.neoforge_pr}"
                content { includeModule("net.neoforged", "neoforge") }
            }
        }
        maven { url "https://maven.shedaniel.me/" }
        maven { url "https://maven.terraformersmc.com/" }
    }

    def javaVersion = 21
    def javaLanguageVersion = JavaLanguageVersion.of(javaVersion)

    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release = javaVersion
    }

    tasks.withType(JavaExec).configureEach {
        javaLauncher.set(javaToolchains.launcherFor {
            languageVersion = javaLanguageVersion
        })
    }

    java {
        toolchain {
            languageVersion = javaLanguageVersion
        }
        withSourcesJar()
    }

    jar {
        from "LICENSE"
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        accessWidenerPath = project(":common").file "src/main/resources/freecam.accesswidener"

        runs {
            client {
                // Pretty run-config name
                // Note: we can't disable the (:path) suffix
                // https://github.com/architectury/architectury-loom/blob/5b3e7c72b665f7eb38c23ceb677027acdc867398/src/main/java/net/fabricmc/loom/configuration/ide/RunConfig.java#L117
                configName "Run"
            }
            remove server // We only need client runs
        }

        silentMojangMappingsLicense()
    }

    dependencies {
        def parchmentAppendix = rootProject.parchment_version.split('-')[0]
        def parchmentVersion = rootProject.parchment_version.split('-')[1]

        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${parchmentAppendix}:${parchmentVersion}@zip")
        }
    }

    publishing {
        repositories {
            mavenLocal()
        }

        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }
}
