plugins {
    alias(libs.plugins.moddev)
    alias(libs.plugins.fletchingtable.neoforge)
    id("freecam.loaders")
}

val processedAw = stonecutterBuild.process(project(":common").file("src/main/resources/freecam.accesswidener"), "build/stonecutter/processed.aw");

fletchingTable {
    // Convert accesswidener to META-INF/accesstransformer.cfg
    accessConverter.register(sourceSets.main) {
        add(processedAw.name)
    }
}

neoForge {
    enable {
        version = currentMod.dep("neoforge")
    }
}

dependencies {
    api("me.shedaniel.cloth:cloth-config-neoforge:${currentMod.dep("cloth")}")
    jarJar(implementation("me.shedaniel.cloth:cloth-config-neoforge:${currentMod.dep("cloth")}") as Any)
}

neoForge {
    // Use the accesstransformer generated by Fletching Table
    accessTransformers.from(tasks.processResources.map {
        it.destinationDir.resolve("META-INF/accesstransformer.cfg")
    })
    validateAccessTransformers = true

    runs {
        register("client") {
            client()
            ideName = "NeoForge Client (${project.path})"
        }
//        register("server") {
//            server()
//            ideName = "NeoForge Server (${project.path})"
//        }
    }

    parchment {
        currentMod.parchment { mappings, mc ->
            minecraftVersion = mc
            mappingsVersion = mappings
        }
    }

    mods {
        register(currentMod.id) {
            sourceSet(sourceSets.main.get())
        }
    }
}

sourceSets.main {
    resources.srcDir(processedAw.parentFile)
    resources.srcDir("src/generated/resources")
}

tasks.register<Copy>("buildAndCollect") {
    group = "build"
    from(tasks.jar.map { it.archiveFile })
    into(rootProject.layout.buildDirectory.file("libs/${currentMod.version}"))
    dependsOn(tasks.build)
}

tasks.named("createMinecraftArtifacts") {
    dependsOn(":neoforge:${project.name}:processResources")
}

tasks.processResources {
    exclude("freecam.accesswidener")

    filesMatching(listOf("META-INF/mods.toml", "META-INF/neoforge.mods.toml")) {
        expand(commonExpansions)
    }

    filesMatching("pack.mcmeta") {
        expand(commonJsonExpansions)
    }

    inputs.properties(commonExpansions)
}

publisher {
    artifact.set(tasks.named("jar"))
}
