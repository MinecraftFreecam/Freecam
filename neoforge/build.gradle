plugins {
    id "com.github.johnrengelman.shadow"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

dependencies {
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"

    modApi "me.shedaniel.cloth:cloth-config-neoforge:${rootProject.cloth_version}"
    include "me.shedaniel.cloth:cloth-config-neoforge:${rootProject.cloth_version}"

    common project(path: ":common", configuration: "namedElements")
    shadowCommon project(path: ":common", configuration: "transformProductionNeoForge")
}

processResources {
    inputs.properties(
            mod_id: rootProject.name,
            version: project.version,
            authors: project.authors,
            description: project.description,
            licence: project.licence,
            homepage_url: project.homepage_url,
            source_code_url: project.source_code_url,
            issue_tracker_url: project.issue_tracker_url,
            gh_releases_url: project.gh_releases_url,
            curseforge_url: project.curseforge_url,
            modrinth_url: project.modrinth_url,
            crowdin_url: project.crowdin_url,
            neoforge_mc_req: project.neoforge_mc_req,
            neoforge_loader_req: project.neoforge_loader_req,
            neoforge_req: project.neoforge_req,
    )

    def overrides = new TreeMap()
    overrides.mod_id = inputs.properties.mod_id.toLowerCase()
    overrides.name = overrides.mod_id.capitalize()

    filesMatching("META-INF/neoforge.mods.toml") {
        expand inputs.properties + overrides
    }
}


// Configure jar tasks
shadowJar {
    group = "shadow"
    exclude "architectury.common.json"

    manifest {
        inheritFrom tasks.jar.manifest
    }

    configurations = [
            project.configurations.shadowCommon,
    ]

    archiveClassifier = "dev-shadow"
}

remapJar {
    group = "loom"
    inputFile = tasks.shadowJar.archiveFile

    // Transform the AccessWidener into an AccessTransformer
    atAccessWideners.add loom.accessWidenerPath.get().asFile.name
    // Output in root `build/libs`
    destinationDirectory = rootProject.layout.buildDirectory.dir "libs"
}

tasks.build.dependsOn tasks.remapJar

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
