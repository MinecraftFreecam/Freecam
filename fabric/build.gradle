plugins {
    id "com.gradleup.shadow"
    id "com.hypherionmc.modutils.modpublisher"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"

    modImplementation ("com.terraformersmc:modmenu:${rootProject.modmenu_version}") {
        exclude module: "fabric-api"
    }

    modApi("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    include "me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_version}"

    common project(path: ":common", configuration: "namedElements")
    shadowBundle project(path: ":common", configuration: "transformProductionFabric")
}

processResources {
    inputs.properties(
            mod_id: rootProject.name,
            version: project.version,
            authors: project.authors,
            description: project.description,
            licence: project.licence,
            homepage_url: project.homepage_url,
            source_code_url: project.source_code_url,
            issue_tracker_url: project.issue_tracker_url,
            gh_releases_url: project.gh_releases_url,
            curseforge_url: project.curseforge_url,
            modrinth_url: project.modrinth_url,
            crowdin_url: project.crowdin_url,
            fabric_loader_req: project.fabric_loader_req,
            fabric_mc_req: project.fabric_mc_req,
    )

    def overrides = new TreeMap()
    overrides.mod_id = inputs.properties.mod_id.toLowerCase()
    overrides.name = overrides.mod_id.capitalize()
    overrides.json_authors = inputs.properties.authors.split(',').collect { "\"$it\"" }.join(", ")

    filesMatching("fabric.mod.json") {
        expand inputs.properties + overrides
    }
}

// Configure jar tasks
shadowJar {
    group = "shadow"
    exclude "architectury.common.json"

    configurations = [
            project.configurations.shadowBundle,
    ]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    group = "loom"
    inputFile = tasks.shadowJar.archiveFile
    injectAccessWidener = true
    // Output in root `build/libs`
    destinationDirectory = rootProject.layout.buildDirectory.dir "libs"
}

tasks.build.dependsOn tasks.remapJar

publisher {
    [ curseDepends, modrinthDepends ].each {
        it.required "fabric-api"
        it.optional "modmenu"
    }
}
