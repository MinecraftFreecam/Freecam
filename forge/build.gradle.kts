plugins {
    alias(libs.plugins.moddev.legacy)
    alias(libs.plugins.fletchingtable)
    id("freecam.loaders")
    id("freecam.atremapper")
}

val processedAw = stonecutterBuild.process(project(":common").file("src/main/resources/freecam.accesswidener"), "build/stonecutter/processed.aw");

fletchingTable {
    j52j.register("main") {
        extension("json", "**/*.json5")
    }

    accessConverter.register("main") {
        // Access widener file relative to `src/main/resources`
        // Converted to `META-INF/accesstransformer.cfg` by default
        add(processedAw.name)
    }
}

legacyForge {
    enable {
        forgeVersion = "${mod.mc}-${currentMod.dep("forge")}"
    }
}

dependencies {
    compileOnlyApi("org.jetbrains:annotations:26.0.2")
    annotationProcessor("org.spongepowered:mixin:${currentMod.dep("mixin")}:processor")
    forgeDependency(group = "me.shedaniel.cloth", name = "cloth-config-forge", version = currentMod.dep("cloth"))
}

legacyForge {
    // Use the accesstransformer generated by Fletching Table
    accessTransformers.from(tasks.processResources.map {
        it.destinationDir.resolve("META-INF/accesstransformer.cfg")
    })
    validateAccessTransformers = true

    runs {
        register("client") {
            client()
            ideName = "Forge Client (${project.path})"
        }
//        register("server") {
//            server()
//            ideName = "Forge Server (${project.path})"
//        }
    }

    parchment {
        currentMod.parchment { mappings, mc ->
            minecraftVersion = mc
            mappingsVersion = mappings
        }
    }

    mods {
        register(currentMod.id) {
            sourceSet(sourceSets.main.get())
        }
    }
}

mixin {
    add(sourceSets.main.get(), "mixins.freecam.refmap.json")
    config("freecam-common.mixins.json")
    config("freecam-forge.mixins.json")
}

sourceSets.main {
    resources.srcDir(processedAw.parentFile)
    resources.srcDir("src/generated/resources")
}

tasks.register<Copy>("buildAndCollect") {
    group = "build"
    from(tasks.jar.map { it.archiveFile })
    into(rootProject.layout.buildDirectory.file("libs/${currentMod.version}"))
    dependsOn("build")
}

tasks.processResources {
    exclude("freecam.accesswidener")

    filesMatching(listOf("META-INF/mods.toml", "META-INF/forge.mods.toml")) {
        expand(commonExpansions)
    }

    filesMatching("freecam-forge.mixins.json") {
        expand(commonJsonExpansions)
    }

    filesMatching("pack.mcmeta") {
        expand(commonJsonExpansions)
    }

    inputs.properties(commonExpansions)
}

tasks.jar {
    finalizedBy("reobfJar")
}

publisher {
    artifact.set(tasks.named("jar"))
}

fun DependencyHandlerScope.forgeDependency(group: String, name: String, version: String) {
    compileOnly(group, name, version)
    modRuntimeOnly(group, name, version)
    jarJar(modImplementation(group, name, version))
}